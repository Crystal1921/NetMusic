buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = '1.1.0'
group = 'com.github.tartaricacid'
archivesBaseName = 'netmusic-1.12.2'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    accessTransformer = file('src/main/resources/META-INF/netmusic_at.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2859'
    implementation group: 'com.googlecode.soundlibs', name: 'mp3spi', version: '1.9.5.4'
}

jar {
    manifest {
        // 打包 Access Transformers 文件
        attributes 'FMLAT': 'netmusic_at.cfg'
        // 将 CodecJLayerMP3 库打包进行模组中
        attributes 'ContainedDeps': "jlayer-1.0.1.4.jar mp3spi-1.9.5.4.jar tritonus-share-0.3.7.4.jar"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// 修正无法加载资源文件的问题
// https://stackoverflow.com/questions/70118942/building-mod-does-not-include-mcmod-info-1-12-2
apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = true
    }
}
subprojects {
    apply plugin: 'idea'
}

task prepareAssets(type: Copy) {
    group = 'build'
    from project.file('src/main/resources')
    into project.file('build/classes/java/main')
}

classes.dependsOn(prepareAssets)