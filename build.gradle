buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

version = '1.1.1'
group = 'com.github.tartaricacid'
archivesBaseName = 'netmusic-1.20'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.20'
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'netmusic'
            mods {
                netmusic {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'netmusic'
            mods {
                netmusic {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'netmusic'
            mods {
                netmusic {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'netmusic', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                netmusic {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.20-46.0.14'
    minecraftLibrary 'com.googlecode.soundlibs:mp3spi:1.9.5.4'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    dependencies {
        include(dependency("com.googlecode.soundlibs:mp3spi:1.9.5.4"))
        include(dependency("com.googlecode.soundlibs:jlayer:1.0.1.4"))
        include(dependency("com.googlecode.soundlibs:tritonus-share:0.3.7.4"))
    }

    relocate 'javazoom.jl', 'com.github.tartaricacid.netmusic.soundlibs.javazoom.jl'
    relocate 'javazoom.spi', 'com.github.tartaricacid.netmusic.soundlibs.javazoom.spi'
    relocate 'org.tritonus', 'com.github.tartaricacid.netmusic.soundlibs.org.tritonus'

    mergeServiceFiles()
}

reobf {
    shadowJar {}
}

task shadowJarPublish(type: Jar) {
    dependsOn reobfShadowJar
    archiveClassifier = "all"
    from zipTree("${project.projectDir}/build/reobfShadowJar/output.jar")
}

assemble.dependsOn shadowJar
tasks.build.dependsOn shadowJarPublish
